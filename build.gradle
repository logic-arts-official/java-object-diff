plugins {
	id 'java'
	id 'groovy'
	id 'java-library'
	id 'jacoco'
	id 'com.github.ben-manes.versions' version '0.50.0'
	id 'net.researchgate.release' version '3.0.2'
	id 'com.github.jk1.dependency-license-report' version '2.5'
	id 'ru.vyarus.animalsniffer' version '1.7.1'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'de.danielbechler'

description = """Java Object Diff"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/plugins-release" }
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		groovy {
			srcDirs = []
		}
	}
	test {
		java {
			srcDirs = []
		}
		groovy {
			srcDirs = ['src/test/java']
		}
	}
	intTest {
		groovy {
			srcDirs = ['src/integration-test/java']
		}
	}
}

configurations {
	intTestImplementation.extendsFrom testImplementation
	intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets.intTest.compileClasspath += sourceSets.main.output + sourceSets.test.output
sourceSets.intTest.runtimeClasspath += sourceSets.main.output + sourceSets.test.output

task intTest(type: Test) {
	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
}

check.dependsOn intTest

compileGroovy {
	// somehow the groovy compile deletes the java compiled classes from the build directory
	dependsOn = []
}

jar {
	manifest {
		attributes(
			'Bundle-Vendor': 'Daniel Bechler',
			'Bundle-DocURL': 'https://github.com/logic-arts-official/java-object-diff',
			'Bundle-Name': 'java-object-diff',
			'Bundle-SymbolicName': 'de.danielbechler.diff',
			'Bundle-Version': project.version,
			'Export-Package': 'de.danielbechler.diff.*',
			'Implementation-Title': 'Java Object Diff',
			'Implementation-Version': project.version
		)
	}
}

dependencies {
	signature 'org.codehaus.mojo.signature:java18:1.0@signature'
	// For Android support:
	// signature 'net.sf.androidscents.signature:android-api-level-23:6.0_r3@signature'
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.9'
	testImplementation group: 'org.apache.groovy', name: 'groovy-all', version: '4.0.15', ext: 'pom'
	testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.4.11'
	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.11'
	testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
	testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.14.9'
	testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.3'
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}

javadoc {
	failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "sqisher"
			name "Daniel Bechler"
			url "https://github.com/SQiShER"
		}
	}
	scm {
		url "https://github.com/logic-arts-official/java-object-diff"
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Library to diff and merge Java objects with ease')
				root.appendNode('name', 'java-object-diff')
				root.appendNode('url', 'https://github.com/logic-arts-official/java-object-diff')
				root.children().last() + pomConfig
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username = System.getenv("MAVEN_USERNAME") ?: project.findProperty("ossrhUsername")
				password = System.getenv("MAVEN_PASSWORD") ?: project.findProperty("ossrhPassword")
			}
		}
	}
}

signing {
	def signingKey = System.getenv("GPG_SIGNING_KEY")
	def signingPassword = System.getenv("GPG_SIGNING_PASSWORD")
	if (signingKey && signingPassword) {
		useInMemoryPgpKeys(signingKey, signingPassword)
		sign publishing.publications.mavenJava
	}
}

release {
	tagTemplate = '$name-$version'
}

@SuppressWarnings("GroovyAssignabilityCheck")
static processFileInplace(File file, Closure processText) {
	String text = file.text
	file.write(processText(text))
}

task updateDocs {
	doLast {
		def updateVersion = { String text ->
			text = text.replaceAll('<version>[^<]+</version>', "<version>${version}</version>")
			text = text.replaceAll('de\\.danielbechler:java-object-diff:[0-9-A-Z\\-.]+', "de.danielbechler:java-object-diff:${version}")
			return text
		}
		processFileInplace(file('README.md'), updateVersion)
		processFileInplace(file('docs/maven.md'), updateVersion)
	}
}

afterReleaseBuild.dependsOn updateDocs

wrapper {
	gradleVersion = '8.5'
	distributionType = Wrapper.DistributionType.ALL
}